buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.12.2-20.2.0-BETA19"
group= "minecrafttransportsimulator" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Immersive Vehicles"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
	sourceCompatibility = targetCompatibility = '1.8'
	options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

minecraft {
	version = "1.12.2-14.23.5.2847"
	runDir = "run"
	mappings = "stable_39"
	makeObfSourceJar = false
}

repositories {
	maven { 
		url 'https://mvnrepository.com/artifact/org.jcraft/jorbis'
	}
  maven {
    // location of the maven that hosts JEI files
    name = "Progwml6 maven"
    url = "https://dvs1.progwml6.com/files/maven/"
  }
}

//Configuration that holds jars to include in the jar
configurations {
    extraLibs
}

dependencies {
	//Set sound code bits as extraLibs so they are included into the jar.
  extraLibs group: 'com.googlecode.soundlibs', name: 'jlayer', version: '1.0.1.4'
	extraLibs group: 'org.jcraft', name: 'jorbis', version: '0.0.17'
	
	// compile against the JEI API but do not include it at runtime
  deobfProvided "mezz.jei:jei_1.12.2:4.16.1.302:api"
  // at runtime, use the full JEI jar
  runtime "mezz.jei:jei_1.12.2:4.16.1.302"
	
	//Compile all extraLib jars.
	configurations.compile.extendsFrom(configurations.extraLibs)
}

//Pack-in the extraLibs specified in the dependencies into the jar.
jar {
    from {
            configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
        }
}

//Do file stuffs.  I think this just updates mcmod.info?
processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
