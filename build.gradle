buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
    }
}

//Apply plugins
apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"

//Set file name.
version = "1.12.2-22.1.0-BETA5"
group = "minecrafttransportsimulator"
archivesBaseName = "Immersive Vehicles"

//Options for Java version to compile to.
sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

//Minecraft mappings and run configs.
minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // Channel:   Version:
    // snapshot   YYYYMMDD   Snapshot are built nightly.
    // stable     #          Stables are built at the discretion of the MCP team.
    // official   MCVersion  Official field/method names from Mojang mapping files
    mappings channel: "stable", version: "39-1.12"

    //Run configurations.
    runs {
        client {
            workingDirectory project.file("run")
        }

        server {
            workingDirectory project.file("runServer")
        }
    }
}

repositories {
    //MP3 library.
    maven { url = "https://mvnrepository.com/artifact/org.jcraft/jorbis" }
    //JEI
    maven { url = "https://dvs1.progwml6.com/files/maven/" }
}

//Configuration that holds jars to include in the jar
configurations {
    embed
    compileOnly.extendsFrom(embed)
}

//Specify dependencies.
dependencies {
    //Minecraft to use.
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2859"

    //Set sound code bits as embeds so they are included into the jar.
    embed("com.googlecode.soundlibs:jlayer:1.0.1.4")
    embed("org.jcraft:jorbis:0.0.17")

    //Compile against the JEI API but do not include it at runtime
    compileOnly fg.deobf("mezz.jei:jei_1.12.2:4.16.1.302:api")
    //At runtime, use the full JEI jar
    //Disabled as Access Transformers don"t work on FG higher than 2.3 despite them saying they do.
    //Not a huge issue, as it just prevents using JEI in-dev, and we already have it working so don"t really need to test.
    //runtimeOnly fg.deobf("mezz.jei:jei_1.12.2:4.16.1.302")
}

//Here is where we zip up all embeds and add them to our jar.
jar {
    from {
        configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

//Now compile the mod.
jar.finalizedBy("reobfJar")

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8" // Use the UTF-8 charset for Java compilation
}